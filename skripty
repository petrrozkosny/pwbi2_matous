dim_datumy = 
ADDCOLUMNS(
-- funkce SELECTCOLUMNS() vybira sloupce z tabulky vracene funkci CALENDARAUTO()
-- a prejmenovava je na "datum"
    SELECTCOLUMNS(
        -- funkce vrati sloupec s nazvem [Date]
        CALENDARAUTO()
    ,"datum",[Date])
,"rok",YEAR([datum])
,"kvartal",QUARTER([datum])
,"mesic",MONTH([datum])
,"den",DAY([datum]))

_______________________________________________________________________________________

dim_datumy_svatky = 
ADDCOLUMNS(
ADDCOLUMNS(
-- funkce SELECTCOLUMNS() vybira sloupce z tabulky vracene funkci CALENDARAUTO()
-- a prejmenovava je na "datum"
    SELECTCOLUMNS(
        -- funkce vrati sloupec s nazvem [Date]
        CALENDARAUTO()
    ,"datum",[Date])
,"rok",YEAR([datum])
,"kvartal",QUARTER([datum])
,"mesic",MONTH([datum])
,"den",DAY([datum])),
"je svatek",
SWITCH(TRUE(),
[den] = 1 && [mesic] = 1,TRUE(),
[den] = 1 && [mesic] = 5,TRUE(),
[datum] IN 
-- datumy velikonoc
{
DATE(2023,04,10),
DATE(2022,04,17),
DATE(2019,04,21)
},TRUE()
))

________________________________________________________________________________

dim_datumy_hodiny = 
ADDCOLUMNS(
GENERATE(
ADDCOLUMNS(
-- funkce SELECTCOLUMNS() vybira sloupce z tabulky vracene funkci CALENDARAUTO()
-- a prejmenovava je na "datum"
    SELECTCOLUMNS(
        -- funkce vrati sloupec s nazvem [Date]
        CALENDARAUTO()
    ,"datum",[Date])
,"rok",YEAR([datum])
,"kvartal",QUARTER([datum])
,"mesic",MONTH([datum])
,"den",DAY([datum])),
SELECTCOLUMNS(
GENERATESERIES(0,23,1),
"hodina",[Value])),
"datum a cas",[datum] +TIME([hodina],0,0))

__________________________________________________________________________________

01 suma srazek ruzyne = 

-- TAKTO NE
VAR _1 = CALCULATE(SUM(data[Srazky]),
FILTER(data,data[Lokalita] = "RUZYNE"))

-- TAKTO ANO, POKUD NEFUNGUJE VARIANTA 3
VAR _2 = 
CALCULATE(SUM(data[Srazky]),
FILTER(ALL(data[Lokalita]),data[Lokalita] = "RUZYNE"))
-- TAKTO ANO
VAR _3 = 
CALCULATE(SUM(data[Srazky]),data[Lokalita] = "RUZYNE")

RETURN _3

____________________________________________________________________________________

02 rocni srazky ruzyne = 
VAR _2 = 
CALCULATE(
    SUM(data[Srazky]),
        FILTER(
        ALL(data[Lokalita],data[mesic]),
    data[Lokalita] = "RUZYNE"))

VAR _3 = 
CALCULATE(
    SUM(data[Srazky]),
    // FILTER(ALL(data[Lokalita]),data[Lokalita] = "RUZYNE"),
    data[Lokalita] = "RUZYNE",
    ALL(data[mesic]))

RETURN _3
________________________________________________________________________________________

03 srazky ruzyne bylo teplo = 
-- suma srazek v danem roce, mesici, lokalite RUZYNE, kdy teplota >30

VAR _2 = 
CALCULATE(SUM(data[Srazky]),
FILTER(ALL(data[Lokalita],data[Teplota]),
data[Lokalita] = "RUZYNE" && data[Teplota] >30))

VAR _3 =
CALCULATE(
    SUM(data[Srazky]),
    data[Lokalita] = "RUZYNE",
    data[Teplota] > 30)

RETURN _3

__________________________________________________________________________________________

04 suma srazek ytd ruzyne = 
-- suma srazek v danem roce, za mesice mensi ci rovno aktualnimu
-- mesici v lokalite ruzyne
CALCULATE(
    SUM(data[Srazky]),
data[Lokalita] = "RUZYNE",
data[mesic] <= SELECTEDVALUE(data[mesic]))

________________________________________________________________________________________

05 suma srazek kumulativne ruzyne predchozi rok = 
CALCULATE(
    SUM(data[Srazky]),
data[Lokalita] = "RUZYNE",
data[mesic] <= SELECTEDVALUE(data[mesic]),
data[rok] = SELECTEDVALUE(data[rok]) - 1)

____________________________________________________________________________________

06 pocet dnu neprselo ruzyne = 
-- nechceme brat v potaz hodnoty null
 CALCULATE(
    COUNT(data[Datum]),
    data[Lokalita] = "RUZYNE",
   NOT ISBLANK(data[Srazky]),
data[Srazky] = 0)

_______________________________________________________________________________

07 datum maximalni teploty = 
-- Funkce CALCULATE() pocita minimalni hodnotu ze sloupce datum,
-- tabulky filtrovane vizualem pres rok a mesic
-- a dale filtrovane funkci MAX() na maximalni teplotu
CALCULATE(
    MIN(data[Datum]),
    data[Teplota] =

    -- max() pocita maximalni hodnotu ze sloupecku teplota
    -- prebira filtry vizualu, tj. je to maximalni teplota
    -- v danem mesici a roce
    max(data[Teplota]))

______________________________________________________________________________

08 mesic nejvetsich srazek = 
-- vyskytl se v danem mesici a roku nejdestivejsi den celeho roku?
-- nejdestivejsi den = den s maximalnimi srazkami
-- maximalni srazky v danem roce bez ohledu na mesic
VAR max_srazky =
CALCULATE(
    MAX(data[Srazky]),
    ALL(data[mesic]))
-- pocet datumu v danem mesici a roce s max_srazky =
return

IF(
CALCULATE(
    COUNT(data[Datum]),
    data[Srazky] = max_srazky)>0,1,0)

______________________________________________________________________________________

09 byla ruzyne nejteplejsi lokalita = 
-- byla ruzyne v danem roce a mesici nejteplejsi lokalitou,
-- tj lokalitou s max teplota

-- maximalni teplota v danem roce, mesici
VAR max_teplota = MAX(data[Teplota])

-- maximalni teplota v danem roce, mesici v ruzyne

VAR max_teplota_ruzyne = 
CALCULATE(MAX(data[Teplota]),data[Lokalita] = "RUZYNE")

return
if(max_teplota = max_teplota_ruzyne,"ano","ne")

______________________________________________________________________________________

10 pocet tropickych dnu ruzyne = 
-- pocet datumu v danem roce, mesici, kdy v lokalite ruzyne
-- teplota > 30
CALCULATE(
    COUNT(data[Datum]),
    data[Lokalita] = "RUZYNE",
    data[Teplota] > 30)

________________________________________________________
15 mesic nejvyssich srazek = 
CALCULATE(
    MAX(data[mesic]),
    data[Srazky] = MAX(data[Srazky]))
____________________________________________________________
16 byl rok nejdestivejsi = 

-- suma srazek v dane lokalite a roce
VAR srazky_rok  = SUM(data[Srazky])
-- nejvyssi rocni srazky v dane lokalite 
VAR nejvyssi_rocni_srazky =
    MAXX(
        ALL(data[rok]),
    CALCULATE(SUM(data[Srazky])))

RETURN
IF(srazky_rok = nejvyssi_rocni_srazky,"ANO","NE")

____________________________________________________________________________________________________________

17 srazky mezimesicne ruzyne = 
-- v danem roce, mesici
VAR srazky_ruzyne = 
CALCULATE(
    SUM(data[Srazky]),
    data[Lokalita] = "RUZYNE")
VAR srazky_ruzyne_minuly_mesic = 


CALCULATE(
    SUM(data[Srazky]),
    data[Lokalita] = "RUZYNE"
    ,FILTER(ALL(data[Datum],data[rok],data[mesic])
    ,data[Datum] >= EOMONTH(max(data[Datum]),-2)+1
    && data[Datum] <= EOMONTH(max(data[Datum]),- 1)))

return srazky_ruzyne - srazky_ruzyne_minuly_mesic

___________________________________________________________________________________________

17 srazky mezimesicne ruzyne = 
-- v danem roce, mesici
VAR srazky_ruzyne = 
CALCULATE(
    SUM(data[Srazky]),
    data[Lokalita] = "RUZYNE")
VAR srazky_ruzyne_minuly_mesic = 
CALCULATE(
    SUM(data[Srazky]),
    data[Lokalita] = "RUZYNE",
    FILTER(ALL(data[Datum],data[rok],data[mesic])
    ,data[Datum] >= EOMONTH(max(data[Datum]),-2)+1
    && data[Datum] <= EOMONTH(max(data[Datum]),- 1)))

return srazky_ruzyne - srazky_ruzyne_minuly_mesic

_________________________________________________________________________________________

18 nejteplejsi lokality = 

VAR max_teplota = MAX(data[Teplota])
RETURN
CONCATENATEX(
FILTER(data,data[Teplota] = max_teplota),
data[Lokalita],", ")

______________________________________________________________________________________

19 datumy max teplot = 

VAR max_teplota = MAX(data[Teplota])

RETURN
IF(ISFILTERED(data[rok]),
CONCATENATEX(
    FILTER(data,
    data[Teplota] = max_teplota),
data[Datum],UNICHAR(10) ,
BLANK()))

_______________________________________________________________
20 pocet destivych vikendovych dnu ruzyne = 

-- v danem mesici a roce
CALCULATE(
    COUNT(data[Datum]),
    data[Lokalita] = "RUZYNE",
    data[Srazky] > 0,
    WEEKDAY(data[Datum],2) > 5)

_______________________________________________________21 podil srazek v roce = 

-- podíl sumy srážek v dané lokalitě a roce
-- na sumě srážek v dané lokalitě ve všech letech

VAR suma_vsechny = CALCULATE(SUM(data[Srazky]),ALL(data[rok]))
VAR suma_srazek  = SUM(data[Srazky])

RETURN
suma_srazek / suma_vsechny
